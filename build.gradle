plugins {
    id 'java-library'
    id "edu.wpi.first.GradleRIO" version "2025.2.1"
    id "maven-publish"
    id "org.jreleaser" version "1.15.0"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// set a valid semver version
version = '2.0.9'

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from tasks.javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            // Set artifact details
            groupId = 'ca.team1310'
            artifactId = 'swerve'

            // Include sources and Javadoc JARs
            artifact tasks.javadocJar
            artifact tasks.sourcesJar

            pom {
                name.set('RunnymedeSwerve') // Replace with your library name
                description.set('An FRC Swerve Library by Team 1310')
                url.set('https://github.com/RunnymedeRobotics1310/RunnymedeSwerve')

                licenses {
                    license {
                        name.set('The Apache License, Version 2.0')
                        url.set('http://www.apache.org/licenses/LICENSE-2.0.txt')
                    }
                }

                developers {
                    developer {
                        id.set('ca.team1310') // Replace with your ID
                        name.set('Runnymede Robotics (Team 1310)')
                        email.set('1310.runnymede.robotics@gmail.com')
                    }
                }

                scm {
                    connection.set('https://github.com/RunnymedeRobotics1310/RunnymedeSwerve.git')
                    developerConnection.set('https://github.com/RunnymedeRobotics1310/RunnymedeSwerve.git')
                    url.set('https://github.com/RunnymedeRobotics1310/RunnymedeSwerve')
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }

}

jreleaser {
    project {
        name = 'RunnymedeSwerve'
        description = 'Team 1310 Runymede Robotics Swerve Lib'
        copyright = '2025 Runnymede Robotics, All Rights Reserved.'
    }
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}